# ==================================================================================================
# Script  : mingps.awk
# --------------------------------------------------------------------------------------------------
# Funtion : The aim is to reduce the size of the kml files generated by autopilots (Ardupilot,
#           Pixhawk, ...) and viewed into Google Earth
#           These autopilots generate by default 5 coordinates by second into kml files, which is huge 
# --------------------------------------------------------------------------------------------------
# Usage   : awk [-v STEP=VAL] -f mingps.awk inputFileName > outputFileName
# Example : awk -v STEP=5 -f mingps.awk logsVol.kml > logsVolAllege.kml
# --------------------------------------------------------------------------------------------------
# Author  : Julien COPPOLANI
# ==================================================================================================
BEGIN{
	# Number of the line which contains coordinates
	# Division factor
	if (STEP == 0) {
		STEP=5;
	}
	# Minimum start altitude
	if (ALT_START_MIN == 0) {
		ALT_START_MIN=30;
	}
	into=0;
	start=0;
}
{
	if (into==1) {
		for (i=1; i<=NF; i+=STEP) {
			if (start==1) {
				if (i+STEP>NF) {
					print $i;
				} else {
					printf $i" ";
				}
			} else {
				split($i,a,",");
				if (a[3] > ALT_START_MIN) {
					start = 1;
					if (i+STEP>NF) {
						print "\t\t\t\t"$i;
					} else {
						printf "\t\t\t\t"$i" ";
					}
				}
			}
		}
		start=0
		into=0
	}
	else {
		if ($0 ~ /<coordinates>/) {
			into=1;	
		}
		print;
	}
}
